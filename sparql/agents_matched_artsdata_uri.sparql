PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX schema: <http://schema.org/>
PREFIX ado: <http://kg.artsdata.ca/ontology/>
construct {
    ?agent a ?type ;
    schema:name ?name ;
    schema:url ?url ;
    schema:addressCountry ?country ;
    schema:sameAs ?artsdata_uri ;
    ado:is_flagged_for_review   ?is_flagged_for_review .
}
where {
    {
        select * where {
            graph <http://kg.artsdata.ca/culture-creates/artsdata-planet-iwts/iwts-ca> {
                ?agent a ?type ;
                schema:name ?name .
                OPTIONAL {
                    ?agent   schema:url ?url .
                }
                OPTIONAL {
                    ?agent schema:location/schema:address/schema:addressCountry ?country .
                }
                OPTIONAL {
                    ?agent schema:workLocation/schema:address/schema:addressCountry ?country .
                }
            }
        } limit 10 offset 0
    }
    ?agent a dbo:Agent .
    OPTIONAL {
        graph <http://kg.artsdata.ca/core> {
            ?agent schema:sameAs ?artsdata_uri .
        }
    }
    OPTIONAL {
        graph <http://kg.artsdata.ca/graph/maintenance> {
            ?agent schema:additionalType <http://kg.artsdata.ca/ontology/FlaggedForReview>
            BIND(true as ?is_flagged_for_review)
        }
    }
}